plugins {
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

dependencies {
    implementation project(':instagram-common')
    implementation project(':instagram-domain')
    implementation project(':instagram-application')

    // Release on 22 Jul 2021 (latest)
    // https://github.com/querydsl/querydsl
    implementation 'com.querydsl:querydsl-core:5.0.0'
    implementation 'com.querydsl:querydsl-jpa:5.0.0'
    implementation 'com.querydsl:querydsl-apt:5.0.0' // For converting Entity To QEntity
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation group: 'it.ozimov', name: 'embedded-redis', version: '0.7.2'

    runtimeOnly 'mysql:mysql-connector-java'

    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.bootJar { enabled = false }
tasks.jar { enabled = true }

def querydslDir = "$buildDir/generated/querydsl"

// QueryDSL plugin refers to here
querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslDir
}

// include querydslDir to build target
sourceSets {
    main.java.srcDir querydslDir
}

// Querydsl compile option
// 'compileQuerydsl' or 'gradle build' -> QClass generated in querydslDir
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

// QueryDSL extends compileClasspath
configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl.doFirst {
    if (file(querydslDir).exists()) {
        delete(file(querydslDir))
    }
}

// when JavaCompile task run -> QClass generated in querydslDir
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}
